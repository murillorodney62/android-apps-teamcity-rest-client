buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}
plugins {
    id "com.jfrog.bintray" version "1.0"
}

apply plugin: 'java'
apply plugin: 'kotlin'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "commons-codec:commons-codec:1.10"
    compile 'com.squareup.retrofit:retrofit:1.9.0'
}

group = 'org.jetbrains.teamcity'
if (hasProperty("buildNumber")) {
    version = "0.1.$buildNumber"
}
apply plugin: 'maven-publish'
task sourceJar(type: Jar) {
    from sourceSets.main.kotlin
}
artifacts {
    archives sourceJar
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}
if (hasProperty("bintrayUser")) {
    bintray {
        user = bintrayUser
        key = bintrayApiKey
        publish = true
        publications = ['mavenJava']
        pkg {
            repo = "maven"
            name = "teamcity-rest-client"
            version {
                name = "0.1.$buildNumber"
            }
        }
    }
}
